plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

repositories {
    jcenter()

    maven { url 'https://jitpack.io' }
}

dependencies {
    compile 'com.github.raphw:byte-buddy:3fcd3dceb6'

    compileOnly "com.sparkjava:spark-core:2.1"

    testCompile 'ch.qos.logback:logback-classic:1.1.2'
    testCompile "org.testcontainers:testcontainers:1.1.6"
    testCompile "io.github.openfeign:feign-core:9.3.1"
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompileOnly "org.projectlombok:lombok:1.16.10"

    // for Grapes and IDEA
    testCompileOnly "org.codehaus.groovy:groovy-all:2.4.7"
    testCompileOnly 'org.apache.ivy:ivy:2.4.0'
}

jar {
    manifest {
        attributes(
                'Premain-Class': 'com.example.superagent.Agent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
        )
    }
}

shadowJar {
    baseName = 'agent'
    classifier = ''

    relocate 'net.bytebuddy', 'com.exmaple.net.bytebuddy'
}

test {
    dependsOn shadowJar

    testLogging {
        showStandardStreams true
    }
}

sourceSets.test.resources.srcDir shadowJar.outputs.files.singleFile.parentFile
